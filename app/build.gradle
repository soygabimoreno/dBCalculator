plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
}

android {
    Properties properties = new Properties()
    properties.load(project.file('../local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.getProperty('keystore.storeFile'))
            storePassword properties.getProperty('keystore.storePassword')
            keyAlias properties.getProperty('keystore.keyAlias')
            keyPassword properties.getProperty('keystore.keyPassword')
        }
    }

    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.appacoustic.android.dbcalculator"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 6
        versionName "2.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        density { enableSplit true }
        abi { enableSplit true }
        language { enableSplit false }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            splits.abi.enable = false
            splits.density.enable = false
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    viewBinding.enabled = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'org.koin:koin-androidx-scope:2.1.6'
    implementation 'org.koin:koin-androidx-viewmodel:2.1.6'

    implementation "io.arrow-kt:arrow-core:0.11.0"
    implementation "io.arrow-kt:arrow-syntax:0.11.0"
    kapt "io.arrow-kt:arrow-meta:0.10.5"

    testImplementation 'junit:junit:4.13.1'
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
}

apply plugin: 'com.google.gms.google-services'
