plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    Properties properties = new Properties()
    properties.load(project.file('../local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.getProperty('keystore.storeFile'))
            storePassword properties.getProperty('keystore.storePassword')
            keyAlias properties.getProperty('keystore.keyAlias')
            keyPassword properties.getProperty('keystore.keyPassword')
        }
    }

    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.appacoustic.android.dbcalculator"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 8
        versionName "2.1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        density { enableSplit true }
        abi { enableSplit true }
        language { enableSplit false }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            splits.abi.enable = false
            splits.density.enable = false
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures.viewBinding = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.1'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.1'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'io.insert-koin:koin-androidx-scope:2.2.2'
    implementation 'io.insert-koin:koin-androidx-viewmodel:2.2.2'

    implementation "io.arrow-kt:arrow-core:0.11.0"
    implementation "io.arrow-kt:arrow-syntax:0.11.0"
    kapt "io.arrow-kt:arrow-meta:0.11.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
}
